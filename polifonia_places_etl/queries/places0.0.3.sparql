# Query to extract data from preprocessed file with places related to Music domain
# and build a Knowledge Graph according to Polifonia places ontology, using sparql.anything
#
# from following datasets:
#      -
#      -
#
# Raw file: https://raw.githubusercontent.com/polifonia-project/sonar2021_demo/datasets/places/places.json
# 
# Polifonia KG ontology: https://github.com/polifonia-project/sonar2021_demo/issues/14



# POLIFONIA prefixes
PREFIX core:    <https://w3id.org/polifonia/ON/core/> 
PREFIX mp:      <https://w3id.org/polifonia/ON/musical-performance/>
PREFIX rdf:     <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs:    <http://www.w3.org/2000/01/rdf-schema#>
PREFIX pr:      <https://w3id.org/polifonia/resource/>

# SPARQL anything prefixes
PREFIX fx:  <http://sparql.xyz/facade-x/ns/>
PREFIX xyz: <http://sparql.xyz/facade-x/data/>


CONSTRUCT { 

    # Recording
    ?recordingIRI   a mp:Recording ;
                    core:hasTitle ?titleIRI ;
                    core:hasYoutubeID ?youtubeID ;
                    mp:isRecordingProducedBy ?recordingProcessIRI;
                    mp:hasRecordingPerformer ?artistIRI .

    # Title
    ?titleIRI   a core:Title ;
                rdfs:label ?titleLabel .

    # Agent
    ?artistIRI  a core:Agent ; 
                rdfs:label ?artistLabel ;
                core:hasBirthPlaceCode ?artistBirthPlaceCodeIRI ;
                core:hasStartCareerPlace ?artistCareerStartPlaceIRI .


    # RecordingProcess  
    ?recordingProcessIRI    rdf:type mp:RecordingProcess ;
                            mp:hasSession ?sessionIRI .



    # Session
    ?sessionIRI core:hasType ?sessionTypeIndividualIRI ;
                rdf:type mp:Session ;
                core:hasPlace ?placeIRI ;
                core:hasTimeInterval ?timeIntervalIRI . 
                
    # TimeInterval
    ?timeIntervalIRI    a core:TimeInterval ;
                        core:startTime ?startTime ;
                        core:endTime ?endTime .
    
    # PhysicalSite
    ?placeIRI a core:PhysicalSite ;
              rdfs:label ?placeLabel ;
              core:hasAddress ?addressIRI ;
              core:hasGeometry ?geometryIRI .

    ?artistCareerStartPlaceIRI a core:PhysicalSite ;
            rdfs:label ?artistCareerStartPlace .

    # Address
    ?addressIRI a core:Address ;
                core:fullAddress ?fullAddress .

    # Geometry
    ?geometryIRI    a core:Geometry ;
                    core:lat ?lat ;
                    core:long ?long .
}
    WHERE {

	SERVICE <x-sparql-anything:> {

        # location for json preprocessed file
		fx:properties fx:location "data/places.json"  ;
	  	fx:media-type "application/json" ;
		fx:blank-nodes false    .


        # UNION pattern to avoid nested OPTIONAL complexity, causing timeout issues
        # 
        #   { ?recording xyz:track_id ?recordingID . } UNION { ?recording  xyz:track_id ?recordingID ; xyz:youtube_id ?youtubeID . }
        #       should be equivalent to:
        #   { ?recording  xyz:track_id ?recordingID . OPTIONAL { ?recording xyz:youtube_id ?youtubeID . } }
        #       but faster to resolve


        {
            # Recording with just ID
		    ?recording  xyz:track%5Fid ?recordingID .
        }
        UNION
        {
            # Recording with youtube ID
		    ?recording  xyz:track%5Fid ?recordingID ;
                        xyz:youtube%5Fid ?youtubeID .
        } 
        UNION
        {
            # Recording with title label and title ID
		    ?recording  xyz:track%5Fid ?recordingID ;
                        xyz:title ?titleLabel ;
                        xyz:title%5Ffor%5Firi ?titleID .
        }
        UNION
        {
            # Recording with artists
            ?recording  xyz:track%5Fid ?recordingID ;
                    ?pArtistForIri ?artistID .

		            FILTER(REGEX(STR(?pArtistForIri),".*artist%5Ffor%5Firi%5F[0-9]+$")) .
        }
        UNION 
        {
            # Recording with artists with label
            ?recording  xyz:track%5Fid ?recordingID ;
                    ?pArtist ?artistLabel ;
                    ?pArtistForIri ?artistID .

                    FILTER(REGEX(STR(?pArtist),".*artist%5F[0-9]+$")) .
		            FILTER(REGEX(STR(?pArtistForIri),".*artist%5Ffor%5Firi%5F[0-9]+$")) .
        }
        UNION 
        {
            # Recording with artists with label and birthplace and startplace
            ?recording  xyz:track%5Fid ?recordingID ;
                    ?pArtist ?artistLabel ;
                    ?pArtistForIri ?artistID ;
                    ?pArtistBirthplaceCode ?artistBirthPlaceCode ;
                    ?pArtistCareerStartPlace ?artistCareerStartPlace ;

                    FILTER(REGEX(STR(?pArtist),".*artist%5F[0-9]+$")) .
                    FILTER(REGEX(STR(?pArtistForIri),".*artist%5Ffor%5Firi%5F[0-9]+$")) .
                    FILTER(REGEX(STR(?pArtistBirthplaceCode),".*artist%5Fcountry%5F[0-9]+$")) .
                    FILTER(REGEX(STR(?pArtistCareerStartPlace),".*artist%5Fstart%5F[0-9]+$")) .
        }
        UNION        
        {
            # Recordings with Sessions
            ?recording  xyz:track%5Fid ?recordingID ;
                    xyz:recording%5Fplaces ?recordingSessionList .
                    # Session
                    ?recordingSessionList ?li ?recordingSession .
                    ?recordingSession xyz:session%5Fid ?sessionID ;
                          xyz:session%5Ftype ?sessionType ;
                          xyz:place ?place ;
                          xyz:begin ?startTime ;
                          xyz:end ?endTime .
        }
        UNION
        {
            # Recordings with Session and session Places
            ?recording  xyz:track%5Fid ?recordingID ;
                    xyz:recording%5Fplaces ?recordingSessionList .

            # Session
            ?recordingSessionList ?li ?recordingSession .
            ?recordingSession xyz:session%5Fid ?sessionID ;
                          xyz:session%5Ftype ?sessionType ;
                          xyz:place ?place ;
                          xyz:begin ?startTime ;
                          xyz:end ?endTime .

            # PhysicalSite
            ?place  xyz:id ?placeID .        
        }
        UNION
        {
            # Recordings with Session and session Places, Geometries and labels

            ?recording  xyz:track%5Fid ?recordingID ;
                    xyz:recording%5Fplaces ?recordingSessionList .

            # Session
            ?recordingSessionList ?li ?recordingSession .
            ?recordingSession xyz:session%5Fid ?sessionID ;
                          xyz:session%5Ftype ?sessionType ;
                          xyz:place ?place ;
                          xyz:begin ?startTime ;
                          xyz:end ?endTime .
        
        

            # PhysicalSite
            ?place  xyz:id ?placeID ;
                    xyz:name ?placeLabel ;
                    xyz:address ?fullAddress ;
                    xyz:coordinates ?coordinates .
        


            # Geometry
            ?coordinates xyz:latitude ?lat ;
                         xyz:longitude ?long .

        }

	}



    # generate resources IRIs from string IDs
    BIND(IRI(CONCAT( STR(pr:) , "Recording/", ENCODE_FOR_URI(?recordingID))) AS ?recordingIRI)
    BIND(IRI(CONCAT( STR(pr:) , "Title/", ENCODE_FOR_URI(?titleID))) AS ?titleIRI) # ENCODE_FOR_URI fix issues with unicode char in strings e.g. "wheni\u00e2\u20ac\u2122msixty\u00e2\u20ac\u0090four"
    BIND(IRI(CONCAT( STR(pr:) , "Agent/", ENCODE_FOR_URI(?artistID))) AS ?artistIRI)
    BIND(IRI(CONCAT( STR(pr:) , "CountryCode/", ENCODE_FOR_URI(?artistBirthPlaceCode))) AS ?artistBirthPlaceCodeIRI)
    BIND(IRI(CONCAT( STR(pr:) , "RecordingProcess/", ENCODE_FOR_URI(?recordingID))) AS ?recordingProcessIRI)
    BIND(IRI(CONCAT( STR(pr:) , "Session/", ENCODE_FOR_URI(?sessionID))) AS ?sessionIRI)
    BIND(IRI(CONCAT( STR(pr:) , "TimeInterval/", ENCODE_FOR_URI(?sessionID))) AS ?timeIntervalIRI)
    BIND(IRI(CONCAT( STR(pr:) , "Place/", ENCODE_FOR_URI(?placeID))) AS ?placeIRI)
    BIND(IRI(CONCAT( STR(pr:) , "Place/", ENCODE_FOR_URI(?artistCareerStartPlace))) AS ?artistCareerStartPlaceIRI)
    BIND(IRI(CONCAT( STR(pr:) , "Address/", ENCODE_FOR_URI(?placeID))) AS ?addressIRI)
    BIND(IRI(CONCAT( STR(pr:) , "Geometry/", ENCODE_FOR_URI(?placeID))) AS ?geometryIRI)
    # individuals IRIs from string IRI
    BIND(IRI(?sessionType) AS ?sessionTypeIndividualIRI)

} 
